[ISubPalette, "~Extended:Geomtery"]

/*------------------------------------------------------------------------------

	FUNCTIONS

--------------------------------------------------------------------------------*/

/* extractSubtool
*/
[RoutineDef, extractSubtool
,
	/* function */
	//[VarSet, toolnameorigin, [IGetTitle,Tool:ItemInfo]]
	[VarSet, toolnameorigin, [StrExtract,  [IGetTitle,Tool:ItemInfo], 0, [StrLength,  [IGetTitle,Tool:ItemInfo] ] - 2 ] ] // Remove trailing "." from name
	[ISet,Tool:SubTool:Thick, [Var, thickness] ]

	//[IPress,Tool:SubTool:TBorder]

	[IPress,Tool:SubTool:Extract]

	[IPress,Tool:SubTool:Accept]

	//[VarSet, toolnameextracted, [IGetTitle,Tool:ItemInfo]]
	[VarSet, toolnameextracted, [StrExtract,  [IGetTitle,Tool:ItemInfo], 0, [StrLength,  [IGetTitle,Tool:ItemInfo] ] - 2 ] ] // Remove trailing "." from name

	/* RENAME SUBTOOL  */
	[VarSet, subtoolName, [StrMerge, toolnameorigin, "-EX"] ]
	[VarSet, renameButton,"Tool:SubTool:Rename"]

	[If, (([IExists, renameButton]) && ([IsEnabled, renameButton])),
	  [FileExecute, [Var, ZFILEUTILSDLL], RenameSetNext, subtoolName]
	  [IPress, renameButton]
	]

	[Note, [StrMerge, "TOOL: ", toolnameorigin, "\n\nEXTRACTED: ",  subtoolName, "\n\nTHICKNESS:",  [Var, thickness], "mm"  ]]

,
	/* parameters */
	thickness
]

/*------------------------------------------------------------------------------

	BUTTONS

--------------------------------------------------------------------------------*/

[IButton, "~Extended:Geomtery:ExtractMask","Copy Masked Geomretry to new subtool"
,
	[IPress,Tool:Visibility:HidePt]
	[IPress,Tool:Masking:Clear]
	[IPress,Tool:SubTool:Duplicate]
	[IPress,Tool:Geometry:Del Lower]
	[IPress,Tool:Geometry:Del Hidden]
	[IPress,Tool:SubTool:SelectDown]
	[IPress,Tool:Visibility:ShowPt]
	[IPress,Tool:SubTool:SelectUp]

	/* UNHIDE SOURCE SUBTOOTL */
	[IPress,Tool:SubTool:SelectUp]
	[IPress,Tool:Visibility:ShowPt]

	/* SELECT NEW SUBTOOTL */
	[IPress,Tool:SubTool:SelectDown]

]/*End of ZScript*/

/** Subdivision Min \ Max toggle
  *
  */
[IButton, "~Extended:Geomtery:MinMax","Toggle subdivision level of all subtools between Min\Max level"
,
	[VarSet, currentsubdiv, [IGet, Tool:Geometry:SDiv ]]


	[If, [Var, currentsubdiv] == 1
	, // THEN
		[IPress,Tool:SubTool:All High]

	, // ELSE

		[IPress,Tool:SubTool:All Low]
	]

,/*Disabled*/, 60,/*Hotkey*/,/*Icon*/,48]


/* EXTRACT geometry thickness 1.2mm
*/
[IButton, "~Extended:Geomtery:Extract", "Automate shell extraction.\n\n:Thick by current value of slider.\n\n\nSubTool:Extract\n\n:SubTool:Accept"
,
	[RoutineCall, extractSubtool, [IGet,Tool:SubTool:Thick] ]


,/*Disabled*/,60,/*Hotkey*/,/*Icon*/,48]


/* EXTRACT geometry thickness 1.2mm
*/
[IButton, "~Extended:Geomtery: Extr 1.2", "Automate shell extraction.\n\nThick:-0.012\n\nShell: 1.2mm\nSubTool:Extract\n\n:SubTool:Accept"
,
	[RoutineCall, extractSubtool, -0.012 ]


,/*Disabled*/,60,/*Hotkey*/,/*Icon*/,48]


/* EXTRACT geometry thickness 2mm
*/
[IButton, "~Extended:Geomtery: EXTR 2", "Automate shell extraction.\n\nThick:-0.02\n\nShell: 2mm\nSubTool:Extract\n\n:SubTool:Accept"
,
	[RoutineCall, extractSubtool, -0.02 ]
,/*Disabled*/,60,/*Hotkey*/,/*Icon*/,48]

[ISubPalette, "~Extended:SubTool"]

/* RENAME SUBTOOL
*/
[RoutineDef, renameSubtool
,
	//[RoutineCall, CheckSystem]

	[VarSet, buttonpath, "Tool:SubTool:Rename"]
	//set buttonpath to the button you want
	//"Tool:SubTool:Rename" - for SubTool
	//"Tool:Layers:Rename" - for Layers
	//"Preferences:Transpose Units:Set Units" - for Transpose Units
	[If, (([IExists, buttonpath]) && ([IsEnabled, buttonpath])),
	  [FileExecute, [Var, ZFILEUTILSDLL], RenameSetNext, newname]
	  [IPress, buttonpath]
	]
,
	/* parameters */
	newname
]

[VarDef, namesuffixed, 0]

/* appendSuffixToCurrentSubToolName
*/
[RoutineDef, appendSuffixToCurrentSubToolName
,
	/* function */
	[VarSet, subtoolname, [StrExtract,  [IGetTitle,Tool:ItemInfo], 0, [StrLength,  [IGetTitle,Tool:ItemInfo] ] - 2 ] ]

	[If, [StrLength, suffix ] > 0
	, // THEN
		[VarSet, namesuffixed, [StrMerge, subtoolname, "-", suffix ] ]

	, // ELSE
		[VarSet, namesuffixed, subtoolname]
	]

,
	/* parameters */
	suffix
]



[RoutineDef, SubToolIsVisible,

	[VarSet, isVisible, 0]
	[VarSet,st,[SubToolGetStatus,id]]
	[VarSet,fInd,[SubToolGetFolderIndex,id]]

	[If,(fInd > -1),//it's in a folder

		[VarSet,stFld,[SubToolGetStatus,fInd]]//get folder visibility

		[If,([Val,stFld]&0x2 == 0x2)&&([Val,st]&0x1 == 0x1), [VarSet,isVisible,1] ] , //else no folder

		[If,([Val,st]&0x1 == 0x1), [VarSet,isVisible,1] ]
	]

 ,isVisible,id
]

/*------------------------------------------------------------------------------

	BUTTONS

--------------------------------------------------------------------------------*/



/* RENAME current subttol floating dialog for new name
*/
[IButton, "~Extended:SubTool:Append Prev", "Append current tool to previous"
,
   /* store the current tool ref */
   [VarSet,toolID,[IGet,Tool:Item Info]]



	/*	//switch to the tool you want to append:
		//(this code assumes that the append tool was the most recently loaded) */
   [ISet, Tool:Item Info,[IGetMax,Tool:Item Info]]


	/* get the name of the append tool: */
   [VarSet,appendTool,[IGetTitle,Tool:Current Tool,0]]


   [VarSet,toolIDAppend,[IGet,Tool:Item Info]]


   /* switch back to the main tool: */
   [ISet,Tool:Item Info,#toolID]

   [If, appendTool != toolIDAppend
   , // THEN
		/* append the tool: */
		[IPress,Tool:SubTool:Append]
		[IPress,[StrMerge,"PopUp:",#appendTool]]

   , // ELSE
		[Note, "There is not any tool to append",, 0]

   ]






,/*Disabled*/, 382 ,/*Hotkey*/ ,/*Icon*/,24]



/* RENAME current subttol floating dialog for new name
*/
[IButton, "~Extended:SubTool:Rename", "RENAME CURRENT SUBTOOL - floating dialog"
,
	[VarSet, subtoolname, [StrExtract,  [IGetTitle,Tool:ItemInfo], 0, [StrLength,  [IGetTitle,Tool:ItemInfo] ] - 2 ] ]

	[VarSet, newname, [StrAsk, subtoolname, "RENAME SUBTOOL"]]

	//[Note, newname]
	[If, [StrLength, subtoolname ] > 0
	, // THEN
		[RoutineCall, renameSubtool, newname ]
	]


,/*Disabled*/,56, CTRL+SHIFT+ALT+'R' ,/*Icon*/,24]




/* ADD SUFFIX to current subtool
*/
[IButton, "~Extended:SubTool:Suffix", "ADD SUFFIX TO CURRENT SUBTOOL NAME - floating dialog"
,
	[VarSet, subtoolname, [StrExtract,  [IGetTitle,Tool:ItemInfo], 0, [StrLength,  [IGetTitle,Tool:ItemInfo] ] - 2 ] ]

	[VarSet, suffix, [StrAsk, "", "SUBTOOL SUFFIX"]]


	[If, [StrLength, subtoolname ] > 0
	, // THEN
		[RoutineCall, appendSuffixToCurrentSubToolName, suffix]

		[RoutineCall, renameSubtool, namesuffixed ]
	]

,/*Disabled*/,48, /*Hotkey*/ ,/*Icon*/,24]


/* ADD SUFFIX to current subtool
*/
[IButton, "~Extended:SubTool:DECI", "RENAME CURRENT SUBTOOL\n\nSUFFIX: 'DECI'"
,
	[VarSet, subtoolname, [StrExtract,  [IGetTitle,Tool:ItemInfo], 0, [StrLength,  [IGetTitle,Tool:ItemInfo] ] - 2 ] ]
	[RoutineCall, renameSubtool, [StrMerge, subtoolname, "-DECI"] ]

,/*Disabled*/,32,/*Hotkey*/,/*Icon*/,24]
/* ADD SUFFIX to current subtool
*/
[IButton, "~Extended:SubTool:SUPPORT", "RENAME CURRENT SUBTOOL\n\nSUFFIX: 'SUPPORT'"
,
	[VarSet, subtoolname, [StrExtract,  [IGetTitle,Tool:ItemInfo], 0, [StrLength,  [IGetTitle,Tool:ItemInfo] ] - 2 ] ]
	[RoutineCall, renameSubtool, [StrMerge, subtoolname, "-SUPPORT"] ]

,/*Disabled*/,32,/*Hotkey*/,/*Icon*/,24]

/* ADD SUFFIX to current subtool
*/
[IButton, "~Extended:SubTool:-5%", "RENAME CURRENT SUBTOOL\n\nSUFFIX: '5%'"
,
	[VarSet, subtoolname, [StrExtract,  [IGetTitle,Tool:ItemInfo], 0, [StrLength,  [IGetTitle,Tool:ItemInfo] ] - 2 ] ]
	[RoutineCall, renameSubtool, [StrMerge, subtoolname, "-5%"] ]

,/*Disabled*/,32,/*Hotkey*/,/*Icon*/,24]

/* ADD SUFFIX to current subtool
*/
[IButton, "~Extended:SubTool:-20%", "RENAME CURRENT SUBTOOL\n\nSUFFIX: '20%'"
,
	[VarSet, subtoolname, [StrExtract,  [IGetTitle,Tool:ItemInfo], 0, [StrLength,  [IGetTitle,Tool:ItemInfo] ] - 2 ] ]
	[RoutineCall, renameSubtool, [StrMerge, subtoolname, "-20%"] ]
,/*Disabled*/,32,/*Hotkey*/,/*Icon*/,24]

/* ADD SUFFIX to current subtool
*/
[IButton, "~Extended:SubTool:-50%", "RENAME CURRENT SUBTOOL\n\nSUFFIX: '50%'"
,
	[VarSet, subtoolname, [StrExtract,  [IGetTitle,Tool:ItemInfo], 0, [StrLength,  [IGetTitle,Tool:ItemInfo] ] - 2 ] ]
	[RoutineCall, renameSubtool, [StrMerge, subtoolname, "-50%"] ]

,/*Disabled*/,32,/*Hotkey*/,/*Icon*/,24]


/* Copy Current Subtool Name
*/
[IButton, "~Extended:SubTool:Copy", "COPY NAME CURRENT SUBTOOL"
,

	[If, ! [MemGetSize, SUBTOOLNAME]
	, // THEN memory exists

	//	[VarDef, memoryString ]
	//
	//	[MemReadString, SUBTOOLNAME, memoryString, 0 /* offset */ ]
	//
	//	[Note, memoryString,, 0]
	//
	//	[MemDelete, SUBTOOLNAME]
	//
	//, // ELSE create memory block
	//
		[VarSet, subtoolNameCurrent, [FileNameExtract, [GetActiveToolPath], 2]]

		[MemCreate, SUBTOOLNAME, 256 /* size */ ]

		[MemWriteString, SUBTOOLNAME, subtoolNameCurrent /* value */ ]

	]


,/*Disabled*/,44,/*Hotkey*/,/*Icon*/, /*width*/]

/* Paste Current Subtool Name
*/
[IButton, "~Extended:SubTool:Paste Name", "PASTE NAME CURRENT SUBTOOL"
,

	[If, [MemGetSize, SUBTOOLNAME]
	, // THEN memory exists

		[VarDef, memoryString, "" ]

		[MemReadString, SUBTOOLNAME, memoryString, 0 /* offset */ ]

		[RoutineCall, renameSubtool, memoryString ]

	, // ELSE create memory block

		[VarSet, toolNameCurrent, [FileNameExtract, [ToolGetPath], 2]]
		[VarSet, subtoolNameCurrent, [FileNameExtract, [GetActiveToolPath], 2]]

		[If, [MessageOKCancel, [StrMerge, "SUBTOOL WAS NOT COPIED RENAME ? FROM: '", subtoolNameCurrent , "'						TO:   '\n", toolNameCurrent, "'" ] ]
		, // THEN
			[RoutineCall, renameSubtool, toolNameCurrent ]
		]

	]

	/* OPEN RENAME DIALOG AFTER NAME PASTED */
	[If, [IExists, "~Extended:SubTool:Rename"], // THEN
		[IPress,"~Extended:SubTool:Rename"]
	, // ELSE
		[IPress,Tool:SubTool:Rename]]





,/*Disabled*/,84,/*Hotkey*/,/*Icon*/, /*width*/]


/* MULTIPLE APPEND
*/
[IButton, "~Extended:SubTool:Multi", "Multi Append",
	[IPress, Zplugin:SubTool Master:MultiAppend ]
,/*Disabled*/,84,/*Hotkey*/,/*Icon*/, /*width*/]


/*------------------------------------------------------------------------------
	SELECT SUBTOOL
--------------------------------------------------------------------------------*/
/* 
*/
[IButton, "~Extended:SubTool:Sel First", "Select First Subtool",
	[SubToolSelect, 0 ]
,/*Disabled*/,84,/*Hotkey*/,/*Icon*/, /*width*/]

/* 
*/
[IButton, "~Extended:SubTool:Sel Last", "Select Last Subtool",
	[SubToolSelect, [SubToolGetCount] - 1]
,/*Disabled*/,84,/*Hotkey*/,/*Icon*/, /*width*/]

/*------------------------------------------------------------------------------
	HISTORY
--------------------------------------------------------------------------------*/
/* Nuke undo history script
*/ 
[IButton, "~Extended:SubTool:Del UNDO", "Delete UNDO history for ALL subtools.",

	[IPress,Edit:Tool:DelUH]
	
,	/*Disabled*/,	#W3 /*Width*/,	/*Hotkey */,	/*Icon*/,	#BIG /*Height*/]

	
/* Nuke undo history script
*/ 
[IButton, "~Extended:SubTool: ALL", "Delete ALL UNDO history from ALL subtools.",

	[Loop,[SubToolGetCount],
		[SubToolSelect,n]
		[If, [IsEnabled, "Edit:Tool:DelOlderUH"],[IPress,Edit:Tool:DelOlderUH]]
		[If, [IsEnabled, "Edit:Tool:DelUH"],[IPress,Edit:Tool:DelUH]]
	,n]
	[Note, [StrMerge, [SubToolGetCount], " subtools have had UNDO history deleted."],,2]
	
,	/*Disabled*/,	#W1 /*Width*/,	/*Hotkey */,	/*Icon*/,	#BIG /*Height*/]


[IButton,"~Extended:SubTool:Del Higher ALL", "Delete HIGHER SUBDIVISION from ALL VISIBLE subtools.", [VarSet,activST,[SubToolGetActiveIndex]]//store active subtool
	[VarSet,isVis,0]

	[Loop,[SubToolGetCount],//loop through all subtools
		[SubToolSelect,[Val,n]] //check visibility
		[RoutineCall,SubToolIsVisible,isVis,[Val,n]]

		[If,(isVis), //code for visible subtool
			//[Note,[StrMerge,"SubTool ",[Val,n]," is Visible"]] //
			
			[IPress,Tool:Geometry:Del Higher]

		,//else
			//[Note,[StrMerge,"SubTool ",[Val,n]," is Hidden"]]
		]

	,n]

	//end loop
	[SubToolSelect,activST]//reselect active subtool

 ]//end button

[IButton,"~Extended:SubTool:Del Lower ALL", "Delete HIGHER SUBDIVISION from ALL VISIBLE subtools.", [VarSet,activST,[SubToolGetActiveIndex]]//store active subtool
	[VarSet,isVis,0]

	[Loop,[SubToolGetCount],//loop through all subtools
		[SubToolSelect,[Val,n]] //check visibility
		[RoutineCall,SubToolIsVisible,isVis,[Val,n]]

		[If,(isVis), //code for visible subtool
			//[Note,[StrMerge,"SubTool ",[Val,n]," is Visible"]] //
			
			[IPress,Tool:Geometry:Del Lower]

		,//else
			//[Note,[StrMerge,"SubTool ",[Val,n]," is Hidden"]]
		]

	,n]

	//end loop
	[SubToolSelect,activST]//reselect active subtool

 ]//end button


[IButton,"~Extended:SubTool:SDiv ALL", "Set current Subdivision level by current ALL VISIBLE subtools.", [VarSet,activST,[SubToolGetActiveIndex]]//store active subtool

	[VarSet,isVis,0]
	
	[VarSet,curSubDiv, [IGet, Tool:Geometry:SDiv ] ]
	
	[Loop,[SubToolGetCount],//loop through all subtools
		[SubToolSelect,[Val,n]] //check visibility
		[RoutineCall,SubToolIsVisible,isVis,[Val,n]]

		[If,(isVis), //code for visible subtool
			//[Note,[StrMerge,"SubTool ",[Val,n]," is Visible"]] //
			
			[ISet, Tool:Geometry:SDiv, curSubDiv ] 

		,//else
			//[Note,[StrMerge,"SubTool ",[Val,n]," is Hidden"]]
		]

	,n]

	//end loop
	[SubToolSelect,activST]//reselect active subtool

 ]//end button
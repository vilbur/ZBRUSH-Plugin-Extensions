[ISubPalette, "~Extended:Trays"]


/** 
  */
[RoutineDef,LeftTrayHide
,
	[Loop,5,
		[If,[IHPos,1000,1]>50,
			[IClick,1000]
		,
			[LoopExit]
		]
	]
]

/** 
  */
[RoutineDef,LeftTrayShow
,
	[Loop,5,
		[If,[IHPos,1000,1]<50,
			[IClick,1000]
		,
			[LoopExit]
		]
	]
]

/** 
  */
[RoutineDef,RightTrayHide
,
	[If,[IHPos,1001]+[IWidth,1001]<[IWidth,Title],
		//[Note, "Open" ]
		[Loop,5,
			[If,[IHPos,1001]+[IWidth,1001]<[IWidth,Title],
				[IClick,1001]
			,
				[LoopExit]
			]
		]
	]
]

/** 
  */
[RoutineDef,RightTrayShow
,
	[If,[IHPos,1001]+[IWidth,1001]>=[IWidth,Title],
		[Loop,5,
			[If,[IHPos,1001]+[IWidth,1001]>=[IWidth,Title],
				//[IClick,1001]
				[IClick,1001]
			,
				[LoopExit]
			]
		]
	]
]

/** Hide trays has priprity
  * If any tray is open, then hide first
  */
[RoutineDef,TraysToggle
,
	[VarSet, isopenleft, 	[IHPos,1000,1]>50 ] 
	[VarSet, isopenright, [IHPos,1001]+[IWidth,1001]<[IWidth,Title]]
	
	[If, [Var, isopenleft] ||  [Var, isopenright] 
	, // HIDE TRAYS
		[If, [Var, isopenleft],
			[RoutineCall, LeftTrayHide]]
	
		[If, [Var, isopenright],
			[RoutineCall, RightTrayHide]]
		
	, // SHOW TRAYS
	
		[If, [Var, isopenleft] == 0,
			[RoutineCall, LeftTrayShow]]
	
		[If, [Var, isopenright] == 0,
			[RoutineCall, RightTrayShow]]
	
	]
]



[IButton, "~Extended:Trays:Left Tray Show", "LeftTrayShow",
	[RoutineCall, LeftTrayShow]
]
  
  
[IButton, "~Extended:Trays:Left Tray Hide", "LeftTrayHide",
	[RoutineCall, LeftTrayHide]
,/*Disabled*/,/*Width*/,/*Hotkey*/,/*Icon*/,/*Height*/]
    
	

[IButton, "~Extended:Trays:Right Tray Show", "RightTrayShow",
	[RoutineCall, RightTrayShow]
,/*Disabled*/,/*Width*/,/*Hotkey*/,/*Icon*/,/*Height*/]
  
  
[IButton, "~Extended:Trays:Right Tray Hide", "RightTrayHide",
	[RoutineCall, RightTrayHide]
,/*Disabled*/,/*Width*/,/*Hotkey*/,/*Icon*/,/*Height*/]
    
[IButton, "~Extended:Trays:Trays Toggle", "Trays Toggle",
	[RoutineCall, TraysToggle]
,/*Disabled*/,/*Width*/,/*Hotkey*/,/*Icon*/,/*Height*/]
    
